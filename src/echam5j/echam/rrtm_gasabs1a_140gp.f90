SUBROUTINE RRTM_GASABS1A_140GP (KPROMA,KBDIM,KLEV,ABSS1,OD,TAUSF1,COLDRY,WX,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,FORFAC,JP,JT,JT1,ONEMINUS,&
  &COLH2O,COLCO2,COLO3,COLN2O,COLCH4,CO2MULT,&
  &LAYTROP,LAYSWTCH,LAYLOW,SELFFAC,SELFFRAC,INDSELF,PFRAC)

!     Reformatted for F90 by JJMorcrette, ECMWF, 980714
!     Include longitude loop as in "RADOPT" of Uwe
!         Schulzweida, Jan2001, Marco A. Giorgetta

USE MO_KIND    , ONLY : DP
USE MO_PARRRTM , ONLY : JPBAND   ,JPGPT   ,JPXSEC
USE MO_RRTAB   , ONLY : TRANS    ,BPADE

IMPLICIT NONE


! Argument list
! ============
INTEGER :: KPROMA
INTEGER :: KBDIM
INTEGER :: KLEV

REAL(DP):: ABSS1(KBDIM,JPGPT*KLEV)
REAL(DP):: TAUSF1(KBDIM,JPGPT*KLEV)
!DIR$ NO_CACHE_ALLOC  ABSS1,  TAUSF1

REAL(DP):: OD(KBDIM,JPGPT,KLEV)
REAL(DP):: COLDRY(KBDIM,KLEV)
REAL(DP):: WX(KBDIM,JPXSEC,KLEV)

!- from AER
REAL(DP):: TAUAERL(KBDIM,KLEV,JPBAND)

!- from INTFAC      
REAL(DP):: FAC00(KBDIM,KLEV)
REAL(DP):: FAC01(KBDIM,KLEV)
REAL(DP):: FAC10(KBDIM,KLEV)
REAL(DP):: FAC11(KBDIM,KLEV)
REAL(DP):: FORFAC(KBDIM,KLEV)

!- from INTIND
INTEGER :: JP(KBDIM,KLEV)
INTEGER :: JT(KBDIM,KLEV)
INTEGER :: JT1(KBDIM,KLEV)

!- from PRECISE             
REAL(DP):: ONEMINUS

!- from PROFDATA             
REAL(DP):: COLH2O(KBDIM,KLEV)
REAL(DP):: COLCO2(KBDIM,KLEV)
REAL(DP):: COLO3 (KBDIM,KLEV)
REAL(DP):: COLN2O(KBDIM,KLEV)
REAL(DP):: COLCH4(KBDIM,KLEV)
!REAL(DP):: COLO2 (KBDIM,KLEV)
REAL(DP):: CO2MULT(KBDIM,KLEV)
INTEGER :: LAYTROP(KBDIM)
INTEGER :: LAYSWTCH(KBDIM)
INTEGER :: LAYLOW(KBDIM)

!- from SELF             
REAL(DP):: SELFFAC(KBDIM,KLEV)
REAL(DP):: SELFFRAC(KBDIM,KLEV)
INTEGER :: INDSELF(KBDIM,KLEV)

!- from SP             
REAL(DP):: PFRAC(KBDIM,JPGPT,KLEV)


! Local variables
! ===============

! as in ECMWF-CY23R1
! ------------------
REAL(DP):: TAU   (KBDIM,JPGPT,KLEV)

!     LOCAL INTEGER SCALARS
INTEGER :: IPR, ITR, LAY, IR

!     LOCAL REAL SCALARS
REAL(DP):: ODEPTH, SECANG, TF


! additional source code from version "RADOPT" by Uwe Schulzweida
! vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
INTEGER :: IPLON
INTEGER :: ICL, ICH

!     LOCAL INTEGER ARRAYS
INTEGER :: IXC(KLEV), IXLOW(KBDIM,KLEV), IXHIGH(KBDIM,KLEV)
INTEGER :: IXS(KLEV), IXLOS(KBDIM,KLEV), IXHIGS(KBDIM,KLEV)

DO LAY = 1, KLEV
  ICL = 0
  ICH = 0
  DO IPLON = 1, KPROMA
    IF ( LAY <= LAYTROP(IPLON) ) THEN
      ICL = ICL + 1
      IXLOW(ICL,LAY) = IPLON
    ELSE
      ICH = ICH + 1
      IXHIGH(ICH,LAY) = IPLON
    ENDIF
  ENDDO
  IXC(LAY) = ICL
ENDDO

DO LAY = 1, KLEV
  ICL = 0
  ICH = 0
  DO IPLON = 1, KPROMA
    IF ( LAY <= LAYSWTCH(IPLON) ) THEN
      ICL = ICL + 1
      IXLOS(ICL,LAY) = IPLON
    ELSE
      ICH = ICH + 1
      IXHIGS(ICH,LAY) = IPLON
    ENDIF
  ENDDO
  IXS(LAY) = ICL
ENDDO
! ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
! end of additional source code from "RADOPT"
! IXC,IXS,IXLOW,IXLOS,IXHIGH,IXHIGS are used
! in calls to RRTM_TAUMOLn, n=1:16, see below


!- SECANG is equal to the secant of the diffusivity angle.
SECANG = 1.66_DP

CALL RRTM_TAUMOL1  (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,FORFAC,JP,JT,JT1,&
  &COLH2O,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL2  (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,COLDRY,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,FORFAC,JP,JT,JT1,&
  &COLH2O,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL3  (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,FORFAC,JP,JT,JT1,ONEMINUS,&
  &COLH2O,COLCO2,COLN2O,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL4  (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,ONEMINUS,&
  &COLH2O,COLCO2,COLO3,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL5  (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,WX,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,ONEMINUS,&
  &COLH2O,COLCO2,COLO3,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL6  (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,WX,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,&
  &COLH2O,CO2MULT,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL7  (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,ONEMINUS,&
  &COLH2O,COLO3,CO2MULT,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL8  (KPROMA,KBDIM,KLEV,IXS,IXLOS,IXHIGS,TAU,WX,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,&
  &COLH2O,COLO3,COLN2O,CO2MULT,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL9  (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,ONEMINUS,&
  &COLH2O,COLN2O,COLCH4,LAYSWTCH,LAYLOW,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL10 (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,&
  &COLH2O,PFRAC)
CALL RRTM_TAUMOL11 (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,&
  &COLH2O,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL12 (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,ONEMINUS,&
  &COLH2O,COLCO2,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL13 (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,ONEMINUS,&
  &COLH2O,COLN2O,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL14 (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,&
  &COLCO2,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL15 (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,ONEMINUS,&
  &COLH2O,COLCO2,COLN2O,SELFFAC,SELFFRAC,INDSELF,PFRAC)
CALL RRTM_TAUMOL16 (KPROMA,KBDIM,KLEV,IXC,IXLOW,IXHIGH,TAU,&
  &TAUAERL,FAC00,FAC01,FAC10,FAC11,JP,JT,JT1,ONEMINUS,&
  &COLH2O,COLCH4,SELFFAC,SELFFRAC,INDSELF,PFRAC)

!- Loop over g-channels.
DO LAY = 1, KLEV
  DO IPR = 1, JPGPT
    DO IPLON = 1, KPROMA
      ODEPTH = SECANG * TAU(IPLON,IPR,LAY)
      OD(IPLON,IPR,LAY) = ODEPTH
      TF = ODEPTH/(BPADE+ODEPTH)
      IF (ODEPTH <= 0._DP) TF = 0._DP
      ITR=INT(5.E+03_DP*TF+0.5_DP)
      IR=(LAY-1)*JPGPT+IPR
      ABSS1 (IPLON,IR) = 1._DP - TRANS(ITR)
      TAUSF1(IPLON,IR) = TF
    ENDDO
  ENDDO
ENDDO

RETURN
END SUBROUTINE RRTM_GASABS1A_140GP
