     SUBROUTINE AUFR
!****************************************************************
!
!     AAAAAA  U    U  FFFFFF  RRRRRR
!     A    A  U    U  F       R    R
!     AAAAAA  U    U  FFFFF   RRRRRR
!     A    A  U    U  F       R  RR
!     A    A  UUUUUU  F       R   RR
!
!
!*****************************************************************
!
!-----------------------------------------------------------------
      USE MO_PARAM1
      USE MO_MPI
      USE MO_PARALLEL
      USE MO_COMMO1
      USE MO_UNITS
!
      IMPLICIT NONE
      INTEGER*8 IDATE,ICODE         
      REAL RDT27, RDT28, RDT
      INTEGER i, j, k
!
      IF (p_pe==p_io) THEN
        OPEN(IO_IN_Z370,FILE='Z37000',STATUS='UNKNOWN'                  &
     &               ,ACCESS='SEQUENTIAL',FORM='UNFORMATTED')

        OPEN(IO_IN_Z380,FILE='Z38000',STATUS='UNKNOWN'                  &
     &               ,ACCESS='SEQUENTIAL',FORM='UNFORMATTED')

        REWIND(IO_IN_Z370)
        REWIND(IO_IN_Z380)
!
        READ(IO_IN_Z370,ERR=99,END=99) IDATE                    
        READ(IO_IN_Z370,ERR=99,END=99) RDT27                  
        GOTO 1
   99   RDT27=0.0
        WRITE(IO_STDOUT,*)'NO DATA ON RESTART FILE Z37000 !!!'
    1   READ(IO_IN_Z380,ERR=88,END=88) IDATE                    
        READ(IO_IN_Z380,ERR=88,END=88) RDT28                     
        GOTO 2
   88   RDT28=0.0
        WRITE(IO_STDOUT,*)'NO DATA ON RESTART FILE Z38000 !!!'
    2   WRITE(IO_STDOUT,6000)RDT27,RDT28
 6000   FORMAT(' Z37000 TIMESTEPS : ',F10.0                             &
     &        ,' Z38000 TIMESTEPS : ',F10.0)
!
        IF(RDT27.GT.RDT28)THEN
          IUNIT=IO_IN_Z370
          IFLAG=1
        ELSE
          IUNIT=IO_IN_Z380
          IFLAG=-1
        ENDIF
!
        REWIND(IUNIT)
!
        READ(IUNIT) IDATE                      
        READ(IUNIT) RDT                       
      ENDIF

      CALL p_bcast(IFLAG,p_io)
      CALL p_bcast(IUNIT,p_io)
      CALL p_bcast(IDATE,p_io)
      CALL p_bcast(RDT,p_io)

      LDT=NINT(RDT)
      LYEARS=IDATE/10000
      LMONTS=(IDATE-LYEARS*10000)/100
      LDAYS=IDATE-(LYEARS*10000)-(LMONTS*100)
      IF (ISTART .EQ. 0) LYEARS=-1
!hh      LYEARS=-1
!
!   INTENTIONAL CRASH IN YEAR LY_END
      IF(LYEARS.EQ.LY_END) THEN
         WRITE(IO_STDOUT,*)' INTENTIONAL CRASH AFTER ',                 &
     &                     LYEARS,' YEARS'
         CALL ABSTURZ
      ENDIF
!::
!:: SET YEAR TO LY_START
      IF(LY_START .GE. 0) THEN
       LYEARS=LY_START
       LMONTS=0
       LDAY=0
       WRITE(IO_STDOUT,*)'ATTN !! SET OFFSET --> LYEARS TO: ',LYEARS
      ENDIF
!:: SET MONTH TO LM_START
      IF(LM_START .GE. 0) THEN
       LMONTS=LM_START-1
       LDAY=0
       WRITE(IO_STDOUT,*)'ATTN !! SET OFFSET --> LMONTH TO: ',LMONTH
      ENDIF
!::
      WRITE(IO_STDOUT,6001) IUNIT,LYEARS,LMONTS,LDAYS,LDT
 6001 FORMAT(' READ FROM UNIT : ',I5,                                   &
     &       ' START YEAR,MONTH,DAY,LDT : ',4I10)

!  READ ZONAL VELOCITY COMPONENT
      DO K=1,KE
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,UOO(:,:,K))
      ENDDO
      CALL bounds_exch('u',UOO,'aufr 1')
      uko(:,:,:)=uoo(:,:,:)

!  READ MERIDIONAL  VELOCITY COMPONENT
      DO K=1,KE
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,VOE(:,:,K))
      ENDDO
      CALL bounds_exch('v',VOE,'aufr 2')
      vke(:,:,:)=voe(:,:,:)


!:: UPDATE VELOCITY FIELDS
      CALL OCTIMF
!  READ TEMPERATURE                     
      DO K=1,KE
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,THO(:,:,K))
      ENDDO
      CALL bounds_exch('p',THO,'aufr 3')

!  READ SALINITY                        
      DO K=1,KE
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,SAO(:,:,K))
      ENDDO
      CALL bounds_exch('p',SAO,'aufr 4')

!:: READ 2-D FIELDS
!  READ SEA SURFACE ELEVATION           
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,ZO)
      CALL bounds_exch('p',ZO,'aufr 5')

!  READ SEA SURFACE ELEVATION CHANGE    
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,Z1O)
      CALL bounds_exch('p',Z1O,'aufr 6')

!  READ SEA ICE THICKNESS               
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,SICTHO)
      CALL bounds_exch('p',SICTHO,'aufr 7')

!  READ SEA ICE CONCENTRATION           
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,SICOMO)
      CALL bounds_exch('p',SICOMO,'aufr 8')

!  READ ZONAL SEA ICE VELOCITY                
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,SICUO)
      CALL bounds_exch('u',SICUO,'aufr 9')

!  READ MERIDIONAL SEA ICE VELOCITY                
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,SICVE)
      CALL bounds_exch('v',SICVE,'aufr 10')
!  READ SNOW                                       
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,SICSNO)
      CALL bounds_exch('p',SICSNO,'aufr 11')
!  READ HIBLER ETA/ZETA FIELDS                     
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,HIBETE)
      CALL bounds_exch('s',hibete,'aufr 12')

      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,HIBETO)
      CALL bounds_exch('p',hibeto,'aufr 13')


      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,HIBZETE)
      CALL bounds_exch('s',hibzete,'aufr 14')

      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,HIBZETO)
      CALL bounds_exch('p',hibzeto,'aufr 15')

!  READ VERTICAL DIFFUSIVITY DVO                            
      DO K=1,KEP
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,DVO(:,:,K))
      ENDDO
      CALL bounds_exch('p',dvo,'aufr 16')

!  READ VERTICAL FRICTION AVO
      DO K=1,KEP
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,AVO(:,:,K))
      ENDDO
      CALL bounds_exch('p',avo,'aufr 17')

!  READ VERTICAL VELOCITY (DIAGNOSTIC)
      DO K=1,KEP
      IF(p_pe==p_io) READ(IUNIT) IDATE
      CALL read_slice(IUNIT,WO(:,:,K))
      ENDDO
      CALL bounds_exch('p',wo,'aufr 18')

      DO J=1,JE
       DO I=1,IE
        DVO(I,J,1)=0.
        DVO(I,J,KEP)=0.
        AVO(I,J,1)=0.
        AVO(I,J,KEP)=0.
       ENDDO
      ENDDO
!  READ ICETEMPERATURE                                       
      IF(p_pe==p_io) READ(IUNIT) IDATE,ICODE
      CALL p_bcast(icode, p_io)
      IF (icode.eq.99) then
          CALL read_slice(IUNIT,TICE)
	  CALL bounds_exch('p',tice,'aufr 19')
      else
         tice(:,:)=0.
      endif

!   
      DO 1467 K=1,KE
      DO 1467 J=1,JE
      DO 1467 I=1,IE
!     UKE(I,J,K)=0.
!      SAO(I,J,K)=MAX(SAO(I,J,K),28.)
!      THO(I,J,K)=MAX(THO(I,J,K),-2.)
      SAO(I,J,K)=MIN(SAO(I,J,K),42.)
      THO(I,J,K)=MIN(THO(I,J,K),70.)
!
!     VKO(I,J,K)=0.
!     ZE(I,J)=0.
!     ZO(I,J)=0.
!
1467  CONTINUE
!
      IF(p_pe==p_io) THEN
        REWIND(IUNIT)
!
        CLOSE(IO_IN_Z370)
        CLOSE(IO_IN_Z380)
      ENDIF
!
      RETURN
      END
